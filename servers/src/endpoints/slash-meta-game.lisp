(in-package :Tootsville)

(require 'sb-introspect)

(defun endpoints-page-header ()
  (list "<!DOCTYPE html>
<html><head><title>Services: "
        (string-capitalize (machine-instance))
        " at "
        (long-site-name)
        "</title>
<link rel=\"stylesheet\"
 href=\"https://jumbo.tootsville.org/Assets/Styles/meta-game.services.css\"
 type=\"text/css\">
<script>
function perform_delete() {
alert(\"This software is not trustworthy enough to allow DELETE testing.\");
}
function perform_get(form) {
var uri = \"\";
for (var i = 0; i < form.childNodes.length; ++i) {
  var el = form.childNodes[i];
  if (el.tagName == \"TT\")
  { uri = uri + el.textContent; }
  if (el.tagName == \"FIELDSET\" &&
      el.childNodes[ el.childNodes.length - 1 ].tagName == \"INPUT\")
  { uri = uri + el.childNodes[ el.childNodes.length - 1 ].value; }
}
  window.open(uri);
}
</script>
</head><body>
<h1>Services</h1>

<p> Services browser for this host.  Access to these services is subject
to   the   terms  of   service   of   the   Tootsville  game;   see   <a
href=\"https://tootsville.org/\">Tootsville.org</a>. </p>

<p>  This documentation,  and much  more, can  also be  found in  the <a
href=\"http://goethe.tootsville.org/devel/docs/Tootsville/"
        (romance-ii-program-version)
        "/Tootsville.pdf\">Reference
        Manual</a>. </p>

        <h2>"
        (cluster-name)
        "</h2><h3>"
        (machine-instance)
        "."
        (short-site-name)
        "</h3><ul>"
        #(#\newline #\newline)))

(defun endpoints-page-footer ()
  "</ul></body></html>")

(defun enumerate-routes ()
  (sort
   (sort
    (mapcar (lambda (path)
              (destructuring-bind (method template length accept function) path
                (declare (ignore length))
                (list :method method :template template :content-type accept
                      :fn function
                      :docstring (documentation function 'function))))
            *paths*)
    #'string<
    :key (rcurry #'getf :method))
   #'string<
   :key (lambda (r) (format nil "狺ㄧ弭呼屙痨狒濠┅┅ㄤ彐躅蝈痨徙瀛翦轭骘翎忪弩篝蜷铉篚怏糸趱翦＼篚＼鏖翳秕麴豸麸篝蜷铉铹鏖翳轭瘐舡骝镯篝蜷铉篝蜷铉祜镳骘扉铄戾è蝈徜扉铄铋铋飑┅麒孱篝蜷铉趄轫飑┅躅糸ㄡ钿扉铄冀戾铉翳扉铄┅篝蜷铉羼踽⒗翎忪澧扉铄哄钿订滹痱轭扉铄铹滹翦蝠蜷铹滹躅戾篌扉铄蝈趱蝾骝镯蝈痨徙瀛翦轭骘翎忪弩篝蜷铉┅痱轭⒓潇劲铹戾è漪铋飑祜镳骘扉铄戾è蝈徜扉铄铋铋飑┅麒孱篝蜷铉趄轫飑┅躅糸ㄡ钿扉铄冀卑戾铉翳扉铄┅篝蜷铉羼踽⒗孱翎忪澧扉铄哄钿卑┅滹麒孱扉铄ㄩㄡ钿冀戾铉翳扉铄┅篝蜷铉羼踽⒗轸屙扉铄哄钿旦痱镧ㄩ漪痱轭扉铄铹ㄦ矧磲⒓滗炯漪峻幄扉铄┅箦翩漪舂痱镧ㄩ漪ㄦ矧磲⒓漪炯滗峻幄扉铄痱轭扉铄铹箦翩漪铋飑┅滹翦蝠蜷铹骈钺祆痱镧ㄩ漪痱轭⒓漪炯潇劲铹痱轭⒓滗炯潇劲铹祜镳骘扉铄蝈徜扉铄铋铋飑麒殪扉铄滹痱轭扉铄铹滹翦蝠蜷铹┅┅┅┅ㄤ彐躅滹泱趄轭绛捐繇ㄤ镢篝蜷铉簌礅镬麒孱ㄦ怙躅漯簌礅镬戾è骈蝮舡扉铄篚怏羼滹泱趄轭痫箝糸镱＼五黛轭滹泱趄轭绌┅祜镳骘黠蜾轭筲轭趄矬疱泗烘躅泗轱瞽灬礅溽扉篝ㄦ溴骈铋糸镱簌礅镬┅躅戾篌礤礅弪黠蜾灬礅溽扉篝脲黠蜾螬滹箦翩骈蝮舡扉铄蝈珏蝈痨徙瀛犰ㄣ镱汜翦钺翦篝蜷铉á篝蜷铉躔汜箦ㄥ豉疱汜箦黠蜾ㄡ麸黠蜾ㄣ镱ㄥ豉疱汜箦ㄣ狎黠蜾ㄡ麸ㄣ狎黠蜾┅ㄣ镱ㄣ後黠蜾┅┅┅骈蝮舡扉铄⒗鲠螓苘饼┅箦翩滹泱趄轭ㄣ镱汜翦钺翦篝蜷铉骈蝮舡扉铄篚怏羼滹泱趄轭矧痫箝糸镱＼五黛轭滹泱趄轭绌戾铉翳滹泱趄轭绌┅┅┅麒孱箦狎汨⒗翎忪澧滹泱趄轭绌箦翩滹泱趄轭蝈痨徙瀛翦轭骘翎忪弩滹泱趄轭绌┅ㄣ镱汜翦钺翦篝蜷铉⒓箦泗轱罹拣劲蝈珏蝈痨徙瀛疳轵ЖáΒ犴鸹á饥祠虎ǎ篝蜷铉＼篚猢⒓á苘钴茴⒓鹁拣劲á啜哿诎公荸Б⒓沆狍蠼堍骖蝈孳⒕苘奔饩á栗蜢ó咯⒓栩彐杰④鼙堍拒鼙集峋á荔犴瘥ó咯⒓趑拒鼙集趑劲á丽狎ó咯⒓箴犷沆狍蠼堍鲠蜍⒕苘奔箴犷劲á荔邈糸镱ó咯⒓箦泗轱罹俭邈糸镱炯璩拒鼙集璩劲á荔踱箦泗轱铥ó咯⒓璐拒鼙集璐劲á荔踱篚怏邈糸镱ó咯⒓璧拒鼙集璧劲á厘铛礤蜥翦⒓镬劲á厘钿孱蹴弪狒澧⒓镬劲á篱翦黹澧⒓蹯劲á厘钿轸屙辁澧⒓蹯劲á篱翦恝⒓扉劲滹泱趄轭绌⒓鹁集箦泗轱罹┅ㄤ彐躅溴泔蜥翦礤翳镤梏盱礤翳镤ㄦ矧磲铋⒓箴犷沆狍蠼堍礤翳镤礤翳镤狺┸⒕邯峒箴犷劲礤翳镤┅ㄤ彐躅溴泔蜥翦蝻豸瀛翦眇灬翦梏盱翦眇灬翦鲠蜷徕戾礤翳镤ㄩ铛祆鲠蜷徕戾螬ㄩㄥ耢虹弭礤翳镤ㄦ矧磲铋⒓栩彐杰彳⒕邯峒峋翦眇灬翦翦眇灬翦ㄩ礤礅弪礤翳镤Ж虹弭轰屐弭濠痱镧箦翩翦眇灬翦ㄣ镱汜翦钺翦篝蜷铉⒓骘蝽镱篚忭轸杰疱蜴矧磉篝蜷铉滹黝汜箦礤翳镤翳轶┸⒕翦眇灬翦⒓趑钼箴奸铕豸豉疱杰Ⅲ踱黹糗沆狍蠼堍篚忭轸篝蜷铉滹黝汜箦礤翳镤④钺礤杰⑦堍鲠祯褰堍篝蜷铉汜痖翎扉礤翳镤④⒕集骘蝽炯趑劲┅ㄤ镬轶鲠蜷徕戾鲠蜷徕戾翦眇灬翦箦翩翦眇灬翦蝈珏蝈痨徙ㄣ镱汜翦钺翦篝蜷铉④芎篝蜷铉滹黝汜箦鲠蜷徕戾┅翦眇灬翦ㄦ矧磲铋⒓趑兼殄熹箦艟检彗孱渚检徕屐骘蚪堍彳沆狍蠼堍鲠颦灬忮燔⒕酣邯狺┘灬忮炀集戾珏钿奸铕豸豉疱杰Ⅳ屮糗钺礤杰邯彳⒕集骈屐潴弭炯趑劲鲠蜷徕戾┅┅ㄤ镬轶鲠蜷徕戾鲠蜷徕戾翦眇灬翦箦翩翦眇灬翦蝈珏蝈痨徙ㄣ镱汜翦钺翦篝蜷铉④芎篝蜷铉滹黝汜箦鲠蜷徕戾┅翦眇灬翦ㄦ矧磲铋⒓趑炯箴犷沆狍蠼堍鲠蜍⒕酣狺┘箴犷炯趑劲鲠蜷徕戾┅┅┅ㄤ彐躅蝻豸瀛捐繇蝻豸濠ㄣ镱汜翦钺翦篝蜷铉⒓扉劲ㄤ邈矧狒瀛礤翳镤梏盱ㄧ弭蝻豸喉弭栾洎剪沆狍蠼堍躜檐⒕ㄤ邈矧狒瀛蝻豸瀛翦眇灬翦梏盱ㄦ矧磲铋狺ㄧ弭蝻豸呼屙痨狒濠蝈盹鲥殒铒＇簌礅镬ㄧ弭蝻豸呼屙痨狒濠ㄧ弭蝻豸喉弭栾洎⒓趑尖蚓ㄤ镢篝蜷铉捐繇ㄧ弭蝻豸轰镢篝蜷铉ㄧ弭蝻豸烘瞟⒓扉劲（＼五黛轭濠┅ㄤ彐躅翦眇灬翦撅疱钺痖翦眇灬翦蝈珏蝈痨徙瀛犰④芎ㄛ岘镰诎弓莳ㄦ矧磲铋狺翦眇灬翦灬礅溽麒镬哌磲翥璀篝狎磲翥璀孱哌哌哌ㄤ邈灬蝈ㄩ珙矧哌哌哌哌┅ㄣ镱汜翦钺翦篝蜷铉Ⅺ簌礅镬眭铉弪红轶瓠俱犴屐汜箦篚怏羼麒镬ū磲翥璀篝狎舂磲翥璀孱洎┅┅ㄤ彐躅蝻豸瀛撅疱钺痖蝻豸濠ㄣ桢汶豉疱蝻豸痱镳弪扉篝扉篝篝蜷铉滹黝汜箦ㄧ弭蝻豸喉弭栾洎戾è疳螋獒扉篝狐篚眄狎ㄧ弭蝻豸轰镢篝蜷铉狐蝈箴镱箦簏扉篝⒉鞍扉篝狐溴筱蜷痿轱铧Ⅲ蹉沐篌蝈篚祠狎铒滹沲礤铘邃轭橡孱列泔蝌邈綮韵南溴驷蹯簪扉篝狐溴筱蜷痿轱铧㈡衢祯蝈蝈篚祠狎铒滹沲礤铘邃轭橡孱列泔蝌邈綮韵南┅┅ㄩㄧ弭蝻豸忽狎獒忪弩扉篝狐疳蜥礤翦蝮蝻豸瀛鲠蝮撅疱钺痖蝻豸濠疳螋獒飑疳螋獒飑┅ㄤ彐躅疳翳撅疱钺痖蝻豸瀛珧秕皓ㄤ弩趄蹉趱蜷铉忾钿躜蝈篝蝻豸弩蝻豸瀛珧秕ㄣ桢汶豉疱躜篝蜷铉ㄣ桢汶豉疱蝻豸弩痱镳弪扉篝扉篝翦眇灬翦撅疱钺痖躜椹磲疸犷＇蝻豸瀛撅疱钺痖蝻豸弩┅┅ㄤ彐躅蝻豸瀛鲠蝮撅疱钺痖蝻豸濠祜镳骘鲠轭ㄧ弭蝻豸忽狎獒忪弩泔祆邈糸铉扉篝狐钺礤簌礅镬眭铉弪红轶瓠俱犴屐汜箦鲠颟狐轭Ⅰ蹂蝙狐溴筱蜷痿轱铧篝蜷铉汜痖翎扉鲠颟狐蝈聃轵邃狐筱桢磲扉篝狐豉疱Ⅲ趄轭纰┅┅ㄤ彐躅蝻豸弩痱彐轼邃蝻豸弩戾è磲ㄧ蝻躔怡蝻豸弩呼弩у聃犰弘妁灬礅溽蝻豸濠ㄦ矧磲铋岑"
                                      (butlast
                                       (split-sequence #\/
                                                       (getf route :template))))))))
    (loop for row in map
       for (prefix . routes) = row
       when (search "maintenance" prefix)
       do (setf (cdr row) nil))
    map))

(defun group-plists (plists key &key (test 'eql))
  (let ((hash (make-hash-table :test test)))
    (dolist (plist plists)
      (if (gethash (getf plist key) hash nil)
          (appendf (gethash (getf plist key) hash nil) plist)
          (setf (gethash (getf plist key) hash nil) (list plist))))
    (hash-table-alist hash)))



(defendpoint (get "/meta-game/services" "text/html")
  "Provide a listing of services available in this cluster.

This provides a browseable catalog of  web services that are provided by
this machine or its siblings."
  (list 200 ()
        (reduce (curry #'concatenate 'string)
                (flatten
                 (list 
                  (endpoints-page-header)
                  (mapcar (lambda (prefix-group)
                            (format nil "%<h2>a</h2>狺ㄣ狎痱彐轼珧秕皓磲疸狎＇蝻豸瀛捐繇箫螋箫螋ㄣ潋痱彐轼珧秕皓＇篝蜷铉戾篌弘妁蜚躜蝙＇珏翩喉弭栾洎＇篝蜷铉戾篌弘妁灬礅溽颟ㄦ矧磲铋狺ㄧ弭呼屙痨狒濠┅┅┅箫螋蝻豸弩痱彐轼邃ㄥ铛礤蜥翦蝻豸弩┅＇篝蜷铉戾篌弘妁＇汜颟ㄥ钿痫轭趔疳珏骘雉弪┅┅┅ㄤ彐孱漯镩铘ㄧ弭礤翎玑礤箦蝣殂弩⑨痧扉汜糸镱牦镱⒃栝轶箅弭汨箫螋镦扉篝轭镦箦蝣殂弩轭视衔骘蝽狒翳狒轶铒犷怙澌篝犷溽蜾婶屮轶趔狍篝镳玑礤狍躜躅糸翳橡孱列骘蝽轶黠螂轭铋沐禊扉篝舶ī扉篝后弪鲩沐ㄥ铛礤蜥翦蝻豸弩┅┅ㄤ彐孱漯镩铘ㄧ弭礤翎玑礤箦蝣殂弩躞弪螈⑨痧扉汜糸镱鲱洚镝楫镳孱狃榛鲥蝮轱罱钞阿⑴铛礤蜥翦箦蝣殂弩骘橡孱列僧序秭殇犷橡孱列视衔漉眇镦翳筢礤轭骘蝽狒轱箦孱镱翳轶疳珏怩轭磲汨轭瀛蝈徜徕戾骘蝽狒荔踱箦泗轱铥郁狒躞舶纤澡溽翎蝈趱蝾邃轶轭翳视衔孱泔溴骘蝽镦橡孱列钞爱盎箦栗蜢梏麴蠛镳孱狃轶矧绡骘溴翎殪螽扉篝舶ī觑钺翳犷呼锃牦镱扉篝狐镳孱狃辄⒊爱阿狐轭骘扉篝狐鲥蝮轱铧蝻磲钽瀛殚痱镧蜥憝鲥蝮轱瞟狐糸綮妩蝻磲钽瀛殚痱镧蜥憝钺礤狐扉沐铙妩扉篝狐钺礤⒘切迢尝┅狐箦蝣弪簏扉篝扉篝狐躜禳ㄦ矧磲铋㈣趑痼函躞弪螽岙麸雉篥殪戾矧绡躞弪蟑沆躞翦颡┅狐疳翳簏磲疸犷＇疳翳撅疱钺痖ㄧ蝻躔痨轶趔ㄥ铛礤蜥翦蝻豸弩呼屙痨狒濠狐泔眇镱孱趔（┅┅ㄤ彐孱漯镩铘ㄧ弭礤翎玑礤桢徜弪螈⑨痧扉汜糸镱牦镱⒃栝礤翳镤蝈趱蝾麸翳躞弪翳桢徜弪翳狒蝈徙桢翳狃痨殂狒轱箦蝣弪物翦翳狒翳弩磲栳鲥忮孱盹溟骈邃怡痱秫殄矧祜徜忉灬钽弪轭趄犷箝舢扉篝舶ī扉篝鸿遽溴蝮轭ㄡ扉篝痨轶ㄨ躅汨孱麸雉汉桢徜弪蟓轭┅┅ㄤ彐孱漯镩铘ㄧ弭礤翎玑礤痖铉Ⅳ屮舣痨衢睥⒃栝孱漯镩铘犰麽蝈趱蝾翳喘汨狎徙翦篝蜷铉荔犴瘥痫铉扉篝舶ī痫铉┅